plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.6"
}

sourceCompatibility = 1.8
def ver = '0.1.22'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.github.javaparser:javaparser-core:2.4.0'
    compile 'io.github.lukehutch:fast-classpath-scanner:1.9.21'
    compile 'io.vertx:vertx-core:3.3.0'
    compile 'io.vertx:vertx-mysql-postgresql-client:3.3.0'
    compile 'com.alibaba:fastjson:1.2.13'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'io.vertx:vertx-unit:3.3.0'
    testCompile 'org.slf4j:slf4j-api:1.7.21'
}

javadoc {
    options.encoding = "UTF-8"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.github.notsyncing.lightfur'
            artifactId 'lightfur'
            version ver
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'lightfur'
        licenses = [ 'GPL-3.0' ]
        vcsUrl = 'https://github.com/notsyncing/lightfur.git'

        version {
            name = ver
            vcsTag = ver
        }
    }

    publications = [ 'maven' ]
}