plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.6"
    id "me.champeau.gradle.jmh" version "0.3.1"
}

sourceCompatibility = 1.8
def ver = rootProject.ext.ver

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'io.vertx:vertx-core:3.3.3'
    compile 'io.vertx:vertx-mysql-postgresql-client:3.3.3'
    compile 'com.alibaba:fastjson:1.2.14'
    compile group: 'io.github.lukehutch', name: 'fast-classpath-scanner', version: '2.0.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    //testCompile project(":lightfur-codegen")
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'io.vertx:vertx-unit:3.3.3'
    testCompile 'org.slf4j:slf4j-api:1.7.21'
}

compileJava {
    options.encoding = "utf-8"
}

javadoc {
    options.encoding = "UTF-8"
}

jmh {
    jmhVersion = '1.15'
    profilers = ["gc", "hs_rt"]
    duplicateClassesStrategy = 'warn'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.github.notsyncing.lightfur'
            artifactId 'lightfur-core'
            version ver
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'lightfur-core'
        licenses = [ 'GPL-3.0' ]
        vcsUrl = 'https://github.com/notsyncing/lightfur.git'

        version {
            name = ver
            vcsTag = ver
        }
    }

    publications = [ 'maven' ]
}