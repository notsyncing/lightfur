buildscript {
    ext.kotlin_version = '1.1.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.6"
    id "me.champeau.gradle.jmh" version "0.3.1"
}

apply plugin: "kotlin"

sourceCompatibility = 1.8
def ver = rootProject.ext.ver

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.12"
    compile project(":lightfur-core")
    compile 'com.alibaba:fastjson:1.2.14'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'io.vertx:vertx-unit:3.3.3'
    testCompile 'org.slf4j:slf4j-api:1.7.21'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
    testCompile project(":lightfur-core").sourceSets.test.output

    jmh group: 'org.spf4j', name: 'spf4j-jmh', version: '8.1.6'
}

compileJava {
    options.encoding = "utf-8"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

javadoc {
    options.encoding = "UTF-8"
}

jmh {
    jmhVersion = '1.15'
    jvmArgsAppend = "-XX:+UnlockCommercialFeatures"
    verbosity = "EXTRA"
    profilers = ["gc", "hs_rt", "org.spf4j.stackmonitor.JmhFlightRecorderProfiler"]
    duplicateClassesStrategy = 'warn'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.github.notsyncing.lightfur'
            artifactId 'lightfur-entity'
            version ver
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'lightfur-entity'
        licenses = [ 'GPL-3.0' ]
        vcsUrl = 'https://github.com/notsyncing/lightfur.git'

        version {
            name = ver
            vcsTag = ver
        }
    }

    publications = [ 'maven' ]
}
