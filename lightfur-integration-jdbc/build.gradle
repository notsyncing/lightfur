plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.0"
}

sourceCompatibility = 9
def ver = rootProject.ext.ver

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile project(":lightfur-core")
    compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.7.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'io.vertx:vertx-unit:3.5.0'
    testCompile 'org.slf4j:slf4j-api:1.7.21'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.10.0'
    testCompile project(":lightfur-core").sourceSets.test.output
}

compileJava {
    options.encoding = "utf-8"
}

javadoc {
    options.encoding = "UTF-8"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.github.notsyncing.lightfur'
            artifactId 'lightfur-integration-jdbc'
            version ver
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'lightfur-integration-jdbc'
        licenses = [ 'GPL-3.0' ]
        vcsUrl = 'https://github.com/notsyncing/lightfur.git'

        version {
            name = ver
            vcsTag = ver
        }
    }

    publications = [ 'maven' ]
}