plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.0"
    id "me.champeau.gradle.jmh" version "0.3.1"
}

apply plugin: "kotlin"

sourceCompatibility = 9
def ver = rootProject.ext.ver

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "https://dl.bintray.com/kotlin/kotlinx"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile project(":lightfur-entity")

    testCompile group: 'junit', name: 'junit', version: '4.12'

    jmh group: 'org.spf4j', name: 'spf4j-jmh', version: '8.1.6'
    jmh project(":lightfur-integration-vertx-ql")
}

compileJava {
    options.encoding = "utf-8"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jmh {
    jmhVersion = '1.19'
    verbosity = "EXTRA"
    profilers = ["gc", "hs_rt"]
    duplicateClassesStrategy = 'warn'
}

javadoc {
    options.encoding = "UTF-8"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.github.notsyncing.lightfur'
            artifactId 'lightfur-ql'
            version ver
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'lightfur-ql'
        licenses = [ 'GPL-3.0' ]
        vcsUrl = 'https://github.com/notsyncing/lightfur.git'

        version {
            name = ver
            vcsTag = ver
        }
    }

    publications = [ 'maven' ]
}
